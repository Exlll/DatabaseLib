allprojects {
    group 'de.exlll'
    version '3.4.0'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        mavenCentral()
        maven { url 'https://maven.pkg.github.com/Exlll/ConfigLib' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
    }

    dependencies {
        compileOnly group: 'de.exlll', name: 'configlib-core', version: '2.2.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.6.28'
        testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }
}
buildscript {
    if (project.hasProperty("local_script")) {
        apply from: file(local_script + "/buildscript.gradle"), to: buildscript
    }
}
if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.gradle")
    subprojects {
        shadowJar {
            relocate 'com.zaxxer.hikari', 'de.exlll.databaselib.com.zaxxer.hikari'
            relocate 'org.slf4j', 'de.exlll.databaselib.org.slf4j'
        }
    }
}
project(':databaselib-core') {
    dependencies { implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.5' }
}
project(':databaselib-bukkit') {
    repositories { maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } }
    dependencies { compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT' }
}
project(':databaselib-bungee') {
    repositories { maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } }
    dependencies { compileOnly 'net.md-5:bungeecord-api:1.16-R0.3' }
}

configure([project(':databaselib-bukkit'), project(':databaselib-bungee')]) {
    dependencies { implementation project(':databaselib-core') }
    jar { from { project(':databaselib-core').sourceSets.main.output } }
}
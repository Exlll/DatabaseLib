allprojects {
    group 'de.exlll'
    version '3.2.0'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        mavenCentral()
        maven { url 'https://repo.terraconia.de/artifactory/terraconia-repos' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
    }

    dependencies {
        compileOnly group: 'de.exlll', name: 'configlib-core', version: '2.2.0'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.2.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-suite-api', version: '1.2.0'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    }
}
buildscript {
    if (project.hasProperty("local_script")) {
        apply from: file(local_script + "/buildscript.local.gradle"), to: buildscript
    }
}
if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
    subprojects {
        shadowJar {
            relocate 'com.zaxxer.hikari', 'de.exlll.databaselib.com.zaxxer.hikari'
            relocate 'org.slf4j', 'de.exlll.databaselib.org.slf4j'
        }
    }
}
project(':databaselib-core') {
    dependencies { compile group: 'com.zaxxer', name: 'HikariCP', version: '3.4.5' }
}
project(':databaselib-bukkit') {
    repositories { maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } }
    dependencies { compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT' }
}
project(':databaselib-bungee') {
    repositories { maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } }
    dependencies { compileOnly 'net.md-5:bungeecord-api:1.16-R0.3' }
}

configure([project(':databaselib-bukkit'), project(':databaselib-bungee')]) {
    dependencies { compile project(':databaselib-core') }
    jar { from { project(':databaselib-core').sourceSets.main.output } }
}
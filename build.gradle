allprojects {
    group 'de.exlll'
    version '3.1.0'
}
subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url 'http://exlll.de:8081/artifactory/releases/' }
    }

    configurations {
        bundle
        bundleWithPlugins
    }

    dependencies {
        bundle group: 'com.zaxxer', name: 'HikariCP', version: '3.1.0'
        compile group: 'de.exlll', name: 'configlib-core', version: '2.+'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.2.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-suite-api', version: '1.2.0'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    }
}
buildscript {
    if (project.hasProperty("local_script")) {
        apply from: file(local_script + "/buildscript.local.gradle"), to: buildscript
    }
}
if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

project(':databaselib-core') {
    dependencies {
        compile group: 'com.zaxxer', name: 'HikariCP', version: '3.1.0'
    }
}
project(':databaselib-bukkit') {
    repositories { maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } }
    dependencies {
        compile group: 'org.bukkit', name: 'bukkit', version: '1.12.2-R0.1-SNAPSHOT'
    }
}
project(':databaselib-bungee') {
    repositories { maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } }
    dependencies {
        compile group: 'net.md-5', name: 'bungeecord-api', version: '1.12-SNAPSHOT'
    }
}

configure([project(':databaselib-bukkit'), project(':databaselib-bungee')]) {
    dependencies {
        compile project(':databaselib-core')
    }
    jar {
        from { project(':databaselib-core').sourceSets.main.output }
        from { configurations.bundle.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    task bundleWithPlugins(type: Jar, group: 'build') {
        from { project(':databaselib-core').sourceSets.main.output }
        from { configurations.bundle.collect { it.isDirectory() ? it : zipTree(it) } }
        from { configurations.bundleWithPlugins.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}